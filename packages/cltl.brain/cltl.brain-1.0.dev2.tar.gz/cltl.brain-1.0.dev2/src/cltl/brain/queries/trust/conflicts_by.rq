PREFIX gaf: <http://groundedannotationframework.org/gaf#>
PREFIX grasp: <http://groundedannotationframework.org/grasp#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX graspf: <http://groundedannotationframework.org/grasp/factuality#>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX sem: <http://semanticweb.cs.vu.nl/2009/11/sem/>
prefix leolaniFriends: <http://cltl.nl/leolani/friends/>

SELECT ?g ?datePos ?authorPos ?dateNeg ?authorNeg

WHERE {
    ?g gaf:denotedBy ?mPos .
    ?mPos grasp:hasAttribution ?attPos .
    ?attPos rdf:value graspf:POSITIVE .
    ?attPos rdf:value / rdf:type graspf:PolarityValue .
    ?mPos prov:wasDerivedFrom ?uttPos .
    ?uttPos rdf:type grasp:Utterance .
    ?contPos sem:hasEvent / sem:hasSubEvent ?uttPos .
    ?contPos ?time_predPos ?datePos .
    VALUES (?time_predPos) { (sem:hasTime) (sem:hasBeginTimeStamp) } .
    ?mPos grasp:wasAttributedTo ?authorPos .


    ?g gaf:denotedBy ?mNeg .
    ?mNeg grasp:hasAttribution ?attNeg .
    ?attNeg rdf:value graspf:NEGATIVE .
    ?attNeg rdf:value / rdf:type graspf:PolarityValue .
    ?mNeg prov:wasDerivedFrom ?uttNeg .
    ?uttNeg rdf:type grasp:Utterance .
    ?contNeg sem:hasEvent / sem:hasSubEvent ?uttNeg .
    ?contNeg ?time_predNeg ?dateNeg .
    VALUES (?time_predNeg) { (sem:hasTime) (sem:hasBeginTimeStamp) } .
    ?mNeg grasp:wasAttributedTo ?authorNeg .


    FILTER (?authorPos = <%s> || ?authorNeg = <%s> ) .

    FILTER (STRSTARTS(STR(?authorPos), STR(leolaniFriends:))
            && STRSTARTS(STR(?authorNeg), STR(leolaniFriends:))
    ) .

}
GROUP BY ?g ?datePos ?authorPos ?dateNeg ?authorNeg